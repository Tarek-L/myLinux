# -----------------------------
# fzf helpers
# -----------------------------
fzf_exclude=(
  --exclude .git
  --exclude .npm
  --exclude go
  --exclude node_modules
  --exclude __pycache__
  --exclude .cache
  --exclude .gnupg
  --exclude .local
  --exclude .sage
  --exclude .librewolf
  --exclude .cargo
)

fzfcd() {
  local dir
  dir=$(fd . "${1:-.}" --type d --hidden "${fzf_exclude[@]}" 2>/dev/null | \
    fzf --height=40% --layout=reverse --preview 'tree -a -C {} | head -100' \
        --preview-window=right:50%:wrap \
        --bind 'ctrl-j:down,ctrl-k:up,ctrl-h:toggle-preview,ctrl-l:accept') && cd "$dir"
}

fzfvim() {
  local selected
  selected=$(fd . "${1:-.}" --type f --hidden "${fzf_exclude[@]}" 2>/dev/null | \
    fzf --multi --height=40% --layout=reverse \
        --preview '[[ $(command -v bat) ]] && bat --style=numbers --color=always --line-range=:100 {} || head -n 100 {}' \
        --preview-window=right:50%:wrap \
        --bind 'ctrl-j:down,ctrl-k:up,ctrl-h:toggle-preview,ctrl-l:accept')

  # Only proceed if something was selected
  [[ -n $selected ]] && nvim ${(f)selected}
}

